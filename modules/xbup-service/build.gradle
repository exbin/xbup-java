apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = '1.7'
version = '0.2.0-SNAPSHOT'
group = 'org.exbin.xbup.service'
def libraryName = 'XBUP Service Library'
def libraryDescription = 'Library for XBUP catalog/service'
def libraryUrl = 'http://xbup.exbin.org'
def repositoryName = "xbup-java"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

tasks.withType(JavaCompile) {
    doFirst {
        options.fork = true
        options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
        options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {  
        maven(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact (javadocJar) {
                classifier = 'javadoc'
            }
            
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name libraryName
                    description libraryDescription
                    url libraryUrl
                    scm {
                        url "https://github.com/exbin/${repositoryName}"
                        connection "cm:git:git://github.com/exbin/${repositoryName}.git"
                        developerConnection "scm:git:git@github.com:exbin/${repositoryName}.git"
                    }
                    licenses {
                        license {
                            name 'LGPL license, Version 3.0'
                            url 'https://www.gnu.org/licenses/lgpl.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'HajdaM'
                            name 'Miroslav Hajda'
                            email 'hajdam@users.sf.net'
                        }
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

repositories {
    mavenCentral()
}

// Disable tests on build
check.dependsOn.remove(test)

dependencies {
    compile(project(':modules:xbup-core'))
    compile(project(':modules:xbup-catalog'))
    compile(project(':modules:xbup-client'))
    compile 'javax.persistence:persistence-api:1.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.10'    
}
