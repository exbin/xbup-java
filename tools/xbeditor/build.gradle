apply plugin: 'java'

sourceCompatibility = '1.7'
version = '0.1.24-SNAPSHOT'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.xbup.tool.xbeditor.XBEditor'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    task.args appArgs
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

tasks.withType(JavaCompile) {
    doFirst {
        options.fork = true
        options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
        options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs "../../lib"
    }
}

// Disable tests on build
check.dependsOn.remove(test)

dependencies {
    compile(project(":modules:xbup-core"))
    compile(project(":modules:xbup-editor-base"))
    compile(project(":modules:xbup-editor-module-java_help"))
    compile(project(":modules:xbup-editor-module-online_help"))
    compile(project(":modules:xbup-editor-module-xbdoc_editor"))
    compile ':jspf.core:1.0.2'
    compile 'commons-cli:commons-cli:1.2'
    runtime 'mysql:mysql-connector-java:5.1.30'
    runtime 'org.eclipse.persistence:javax.persistence:2.1.0'
    runtime 'org.eclipse.persistence:eclipselink:2.5.1'
    runtime 'org.apache.derby:derbyclient:10.10.2.0'
    runtime 'org.apache.derby:derby:10.10.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': configurations.compile.collect {'../lib/' + it.getName() }.join(' ')
        )
    }
}
