apply plugin: 'java'

sourceCompatibility = '1.7'
version = '0.2.0-SNAPSHOT'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.xbup.service.XBService'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (!hasProperty('mainClass')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

tasks.withType(JavaCompile) {
//    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
        options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
        options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs "../../lib"
    }
}

// Disable tests on build
check.dependsOn.remove(test)

dependencies {
    compile(project(":modules:xbup-core"))
    compile(project(":modules:xbup-framework-client"))
    compile(project(":modules:xbup-framework-service"))
    compile ':jspf.core:1.0.2'
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.eclipse.persistence:eclipselink:2.5.1'
    runtime 'mysql:mysql-connector-java:5.1.30'
    runtime 'org.eclipse.persistence:javax.persistence:2.1.0'
    runtime 'org.apache.derby:derbyclient:10.10.2.0'
    runtime 'org.apache.derby:derby:10.10.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': configurations.runtime.collect {'../lib/' + it.getName() }.join(' ')
        )
    }
}
